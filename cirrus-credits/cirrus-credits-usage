#!/usr/bin/perl
#
# cirrus-credits-usage - show daily usage
#
package ESM::CirrusCreditsUsage;

use v5.14;
use utf8;
use open qw( :encoding(UTF-8) :std );

use strict;
use warnings;

(our $ME = $0) =~ s|.*/||;
(our $VERSION = '$Revision: 1.7 $ ') =~ tr/[0-9].//cd;

# For debugging, show data structures using DumpTree($var)
#use Data::TreeDumper; $Data::TreeDumper::Displayaddress = 0;

###############################################################################
# BEGIN user-customizable section

our $History = "/home/esm/.cirrus-credits/history.csv";

# END   user-customizable section
###############################################################################

use Date::Parse;
use Text::CSV;
use Time::Piece;

###############################################################################
# BEGIN boilerplate args checking, usage messages

sub usage {
    print  <<"END_USAGE";
Usage: $ME [OPTIONS] ARGS [...]

blah blah blah

OPTIONS:

  -v, --verbose  show verbose progress indicators
  -n, --dry-run  make no actual changes

  --help         display this message
  --man          display program man page
  --version      display program name and version
END_USAGE

    exit;
}

sub man {
    # Read the POD contents.  If it hasn't been filled in yet, abort.
    my $pod = do { local $/; <DATA>; };
    if ($pod =~ /=head1 \s+ NAME \s+ FIXME/xm) {
        warn "$ME: No man page available.  Please try $ME --help\n";
        exit;
    }

    # Use Pod::Man to convert our __DATA__ section to *roff
    eval { require Pod::Man }
        or die "$ME: Cannot generate man page; Pod::Man unavailable: $@\n";
    my $parser = Pod::Man->new(name => $ME, release => $VERSION, section => 1);

    # If called without output redirection, man-ify.
    my $out_fh;
    if (-t *STDOUT) {
        my $pager = $ENV{MANPAGER} || $ENV{PAGER} || 'less';
        open $out_fh, "| nroff -man | $pager";
    }
    else {
        open $out_fh, '>&STDOUT';
    }

    # Read the POD contents, and have Pod::Man read from fake filehandle.
    # This requires 5.8.0.
    open my $pod_handle, '<', \$pod;
    $parser->parse_from_filehandle($pod_handle, $out_fh);
    exit;
}


# Command-line options.  Note that this operates directly on @ARGV !
our $debug   = 0;
our $force   = 0;
our $verbose = 0;
our $NOT     = '';              # print "blahing the blah$NOT\n" if $debug
sub handle_opts {
    use Getopt::Long;
    GetOptions(
        'debug!'     => \$debug,
        'dry-run|n!' => sub { $NOT = ' [NOT]' },
        'force'      => \$force,
        'verbose|v'  => \$verbose,

        help         => \&usage,
        man          => \&man,
        version      => sub { print "$ME version $VERSION\n"; exit 0 },
    ) or die "Try `$ME --help' for help\n";
}

# END   boilerplate args checking, usage messages
###############################################################################

############################## CODE BEGINS HERE ###############################

# The term is "modulino".
__PACKAGE__->main()                                     unless caller();

# Main code.
sub main {
    # Note that we operate directly on @ARGV, not on function parameters.
    # This is deliberate: it's because Getopt::Long only operates on @ARGV
    # and there's no clean way to make it use @_.
    handle_opts();                      # will set package globals

    # Fetch command-line arguments.  Barf if too many.
    die "$ME: Too many arguments; try $ME --help\n"                 if @ARGV;

    doit();
}


sub read_history() {
    my @history;
    my $csv = Text::CSV->new;
    open my $fh, '<', $History
        or die "$ME: Could not read $History: $!\n";
    while (my $row = $csv->getline($fh)) {
        my ($date, $credits) = @$row;

        push @history, [ str2time($date), $credits ];
    }
    close $fh;

    @history;
}

sub doit {
    my @history = read_history();

    # Newline at START, not end, so last line can include balance
    my $newline = '';
    my ($prev_t, $prev_credits);
    for my $row (@history) {
        my ($t, $credits) = @$row;
        if (defined $prev_t) {
            printf "%s%s %5.1f",
                $newline,
                localtime($t)->strftime("%a %b %d %H:%M:%S"),
                $prev_credits - $credits;

            my $delta_t = $t - $prev_t;
            if (abs($delta_t - 86400) > 3610) {
                printf " (** over %.1f hours **)", abs($delta_t/3600.0);
            }

            $newline = "\n";
        }
        $prev_t = $t;
        $prev_credits = $credits;
    }

    # Final line: include balance...
    printf "  balance: %.2f", $prev_credits;

    # ...and, if "running low", an estimate of when we'll run out
    if ($prev_credits < 200 && @history > 10) {
        my $rate = ($history[-10][1] - $prev_credits) /
            (($history[-1][0] - $history[-10][0]) / 86400.0);

        if ($rate > 0) {
            my $t = localtime(time + 86400 * ($prev_credits / $rate));
            printf "; \@ %.2f/day will run out ~%s", $rate, $t->strftime("%m/%d");
        }
    }

    print  "\n";
}


1;

__DATA__

###############################################################################
#
# Documentation
#

=head1	NAME

FIXME - description of what this script does

=head1	SYNOPSIS

FIXME [B<--foo>]  [B<--bar>]  [B<--verbose>] ARG1 [ARG2...] FIXME

FIXME  B<--help>  |  B<--version> | B<--man>

=head1	DESCRIPTION

B<FIXME> grobbles the frobniz on alternate Tuesdays, except where
prohibited by law.

=head1	OPTIONS

=over 4

=item B<--foo>

FIXME

=item B<--verbose>

Show progress messages.

=item B<--help>

Emit usage hints.

=item B<--version>

Display program version.

=item B<--man>

Display this man page.

=back


=head1	DIAGNOSTICS

FIXME

=head1	ENVIRONMENT

FIXME

=head1	FILES

FIXME

=head1	RESTRICTIONS

FIXME

=head1	SEE ALSO

FIXME

e.g. L<Foo::Bar|Foo::Bar>

=head1	AUTHOR

Your Name <ed@edsantiago.com>

Please report bugs or suggestions to <ed@edsantiago.com>

=cut
